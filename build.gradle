import java.text.SimpleDateFormat

group = 'com.nfl.glitr'
version = System.getenv('TRAVIS_TAG') ? PROJECT_VERSION : PROJECT_VERSION + '-' + new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
description = "A library that lets you use Plain Old Java Objects to describe your GraphQL schema."

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.1-rc3"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1'
    }
}

apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

jacoco {
    toolVersion = "0.7.5.201505241946"
}

//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8
//noinspection GroovyUnusedAssignment
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // GraphQL dependencies
    compile("com.graphql-java:graphql-java:7.0")

    // Commons dependencies
    compile("org.apache.commons:commons-lang3:3.4")
    compile("commons-beanutils:commons-beanutils:1.9.2")

    // Google dependencies
    compile("com.google.code.findbugs:jsr305:3.0.0")
    compile("com.google.guava:guava:19.0")
    compile("com.googlecode.gentyref:gentyref:1.2.0")

    // Misc dependencies
    compile("io.reactivex:rxjava:1.1.3")
    compile("javax.validation:validation-api:1.1.0.Final")
    compile("com.fasterxml.jackson.core:jackson-databind:2.6.2")

     // Spock test dependencies
    testCompile("junit:junit:4.12")
    testCompile("cglib:cglib-nodep:3.2.4")  // for spock mocking, need later version for Java 8
    testCompile("org.spockframework:spock-core:1.0-groovy-2.4")
    testCompile("org.codehaus.groovy:groovy-all:2.4.4")
}

task testSpock(type: Test) {
    useJUnit()
}

test.dependsOn 'testSpock'

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

sonarqube {
    //noinspection GroovyAssignabilityCheck
    properties {
        //noinspection GroovyAssignabilityCheck
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/testSpock.exec"
        //noinspection GroovyAssignabilityCheck
        property "sonar.coverage.exclusions", "**/*Exception.java"
        property "sonar.projectKey",  project.ext.app_name
        property "sonar.projectName", project.ext.app_name
        property "sonar.projectVersion", project.version
        property "sonar.host.url", "http://sonar.dm.nfl.com/"
        property "sonar.language", "java"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.scm.provider", "git"
    }
}

//noinspection GroovyAssignabilityCheck
configurations.all {
    // check for updates every build
    // noinspection GroovyAssignabilityCheck
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}


sourceSets {
    //tell gradle where the resource folders (used for inclusion in the jar too)
    main {
        resources.srcDirs = ["src/main/resources", "generatedsources/"]
    }
}

publishing {
    publications {
        projectPublish(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version version

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name project.name
                    description project.description
                    url PROJECT_GITHUB_REPO_URL
                    scm {
                        url PROJECT_GITHUB_REPO_URL
                        connection PROJECT_GITHUB_REPO_URL
                        developerConnection PROJECT_GITHUB_REPO_URL
                    }
                    licenses {
                        license {
                            name 'MIT'
                            url PROJECT_LICENSE_URL
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'tinnou'
                            name 'Antoine Boyer'
                        }
                        developer {
                            id 'vaant'
                            name 'Arash Shokoufandeh'
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER') ?: project.findProperty('bintray.user')
    key = System.getenv('BINTRAY_KEY') ?: project.findProperty('bintray.key')
    publications = ['projectPublish']
    publish = true
    pkg {
        repo = 'maven'
        name = project.name
        userOrg = 'nfl'
        licenses = ['MIT']
        vcsUrl = PROJECT_GITHUB_REPO_URL
        version {
            name = project.version
            desc = project.description
            released  = new Date()
            vcsTag = 'v'+ project.version
            gpg {
                sign = true
            }
            mavenCentralSync {
                sync = project.property('maven.central.sync')
                user = System.getenv('SONATYPE_USERNAME') ?: project.findProperty('sonatype.username')
                password = System.getenv('SONATYPE_PASSWORD') ?: project.findProperty('sonatype.password')
            }
        }
    }
}

// all publish tasks depend on the build task
tasks.withType(PublishToMavenRepository) {
    dependsOn build
}

task sourcesJar(type: Jar) {
    dependsOn classes
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    from "LICENSE"
    manifest {
        attributes(
                "Specification-Title": project.name,
                "Specification-Version": project.version,
                "Implementation-Version": project.version,
        )
    }
}
